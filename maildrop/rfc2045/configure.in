dnl $Id$
dnl
dnl Copyright 1998 - 1999 Double Precision, Inc.  See COPYING for
dnl distribution information.
dnl AC_INIT(rfc2045.c)
dnl AC_PREREQ([2.50])

dnl AC_CONFIG_HEADER(config.h)
dnl AC_SUBST(AR)
dnl AC_SUBST(RANLIB)

#rfc2045 checking.

AC_CACHE_CHECK([for missing gethostname prototype],rfc2045_cv_SYS_GETHOSTNAME,

AC_TRY_COMPILE([
#if HAVE_UNISTD_H
#include	<unistd.h>
#endif

extern int gethostname(int,int);
],[
],rfc2045_cv_SYS_GETHOSTNAME=yes,rfc2045_cv_SYS_GETHOSTNAME=no
)

)

if test $rfc2045_cv_SYS_GETHOSTNAME = "no"
then
  AC_DEFINE_UNQUOTED(HAS_GETHOSTNAME, 1,
         [Define if gethostname() is provided])
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_TYPE_PID_T

dnl Checks for library functions.

RFC2045_CFLAGS="$SHLIB_CFLAGS"
AC_CHECK_FUNC(strncasecmp,,RFC2045_CFLAGS="$RFC2045_CFLAGS -Dstrncasecmp=strnicmp")
AC_CHECK_FUNC(strcasecmp,,RFC2045_CFLAGS="$RFC2045_CFLAGS -Dstrcasecmp=stricmp")

AC_ARG_WITH(rfc2045-package, [], [ rfc2045_package="$withval" ],
  [
    rfc2045_package="$PACKAGE"
    ac_configure_args="$ac_configure_args --with-rfc2045-package=$PACKAGE"
  ])

AC_ARG_WITH(rfc2045-version, [], rfc2045_version="$withval",
  [
    rfc2045_version="$VERSION"
    ac_configure_args="$ac_configure_args --with-rfc2045-version=$VERSION"
  ])

AC_DEFINE_UNQUOTED(RFC2045PKG,"$rfc2045_package",[Specify package])
AC_DEFINE_UNQUOTED(RFC2045VER,"$rfc2045_version",[Specify version])

AC_ARG_ENABLE(mimecharset,
  [  --enable-mimecharset=charset   Default MIME charset to set on new messages],
  RFC2045CHARSET="$enableval", RFC2045CHARSET="us-ascii")

CFLAGS="$CFLAGS -I${srcdir}/.."
AC_SUBST(rfc822includedir)

AC_SUBST(RFC2045CHARSET)

AC_SUBST(RFC2045_CFLAGS)

m4_ifdef([HAS_TOP],[m4_ignore],[
AC_OUTPUT(Makefile rfc2045charset.h)
])
